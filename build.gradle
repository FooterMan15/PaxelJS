plugins {
    id "dev.architectury.loom" version "1.3-SNAPSHOT"
    id "maven-publish"
    id "com.modrinth.minotaur" version "2.+"
}

Properties properties = new Properties()
def propertiesFile = project.rootProject.file('local.properties')
if (propertiesFile.exists()) {
    properties.load(propertiesFile.newDataInputStream())
}
def mr_token = properties.getProperty("modrinth.token")

modrinth {
    token = mr_token
    projectId = "T0fiFtaZ"
    versionNumber = project.version
    versionType = "release"
    uploadFile = remapJar
    gameVersions = ["1.20.1"]
    loaders = ["forge"]
    dependencies {
        required.project("kubejs")
        required.project("cucumber")
    }
}

base {
    archivesName = project.archives_base_name
}

version = project.mod_version
group = project.maven_group

java {
    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_17
}

loom {
    silentMojangMappingsLicense()
    forge {
        mixinConfigs = [
                "paxeljs.mixins.json"
        ]

    }
}

repositories {
    maven {
        url 'https://maven.blakesmods.com'
    }
    maven {
        url = "https://maven.architectury.dev"
        content {
            includeGroup "dev.architectury"
        }
    }

    maven {
        url = "https://maven.saps.dev/minecraft"
        content {
            includeGroup "dev.latvian.mods"
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.officialMojangMappings()
    forge "net.minecraftforge:forge:${project.forge_version}"

    modImplementation("com.blakebr0.cucumber:Cucumber:${minecraft_version}-${cucumber_ver}")
    modImplementation("dev.latvian.mods:kubejs-forge:${kjs_ver}")
    modRuntimeOnly("dev.latvian.mods:rhino-forge:${rhino_ver}")
    modRuntimeOnly("dev.architectury:architectury-forge:${arch_ver}")

    compileOnly(annotationProcessor("io.github.llamalad7:mixinextras-common:0.3.2"))
    implementation(include("io.github.llamalad7:mixinextras-forge:0.3.2"))
}

processResources {
    inputs.property "version", project.version
    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    options.release = 17
}

java {
    withSourcesJar()
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : project.mod_id,
                "Specification-Vendor"    : project.mod_author,
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : version,
                "Implementation-Vendor"   : project.mod_author,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    repositories {
    }
}
